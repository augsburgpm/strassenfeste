#!/usr/bin/env perl

#my $source_url = 'http://www.berlin.de/sen/wirtschaft/service/maerkte/strassenfeste/index.php/index/all.json?q=';
my $source_url = '/all.json';

use Mojolicious::Lite;
use Mojo::JSON 'j';
use Date::Manip::Date;
use List::MoreUtils qw(all);
use Memoize;

sub get_data {
    my $ua = Mojo::UserAgent->new;
    # retrieve data from URL
    my @results = @{j($ua->get('/all.json')->res->body)->{index}};

    # modify
    for my $fest (@results) {

        for my $datename (qw(von bis)) {
            my $date = Date::Manip::Date->new();

            $date->parse_format('%d\\.%m\\.%Y', $fest->{$datename}) && $date->parse_format('%Y\\-%m\\-%d', $fest->{$datename});
            $fest->{$datename} = $date->printf('%d.%m.%Y');
            $fest->{"__$datename"} = $date;
        }
    }
    return \@results;

}
memoize 'get_data';

sub get_filtered_data {
    my ($filters) = @_;
    my $bare_data = get_data();
    # filter
    return [grep(
                 {my $fest = $_;
                  all {$_->($fest)} @$filters;
                 }
                 @$bare_data
                )];
}

# Documentation browser under "/perldoc"
plugin 'PODRenderer';

get '/' => sub {
  my $self = shift;

  my @filters;
  if (my $from = $self->param('filter_from_date')) {
      my $date = Date::Manip::Date->new();
      if (!($date->parse_format('%d\\.%m\\.%Y', $from) && $date->parse_format('%Y\\-%m\\-%d', $from))) {
          push @filters, sub {$_[0]->{__bis}->cmp($date) >= 0};
      }
  }
  if (my $to = $self->param('filter_to_date')) {
      my $date = Date::Manip::Date->new();
      if (!($date->parse_format('%d\\.%m\\.%Y', $to) && $date->parse_format('%Y\\-%m\\-%d', $to))) {
          push @filters, sub {$_[0]->{__von}->cmp($date) <= 0};
      }
  }
  if (my $desc = $self->param('filter_description')) {
      push @filters, sub {index($_[0]->{bezeichnung}, $desc) >= 0};
  }
  $self->stash(feste_data => get_filtered_data(\@filters));
  $self->stash(attrs => [qw[von bis bezeichnung]]);
  $self->render('index');
};

get '/detail/:id' => sub {
    my $self = shift;
    my $id = $self->stash('id');

    my ($fest) = @{get_filtered_data([sub {$_[0]->{id} == $id}])};
    $self->stash('fest', $fest);
    $self->stash(detailattrs => [qw[bezirk bezeichnung strasse plz von bis zeit veranstalter]]);

    $self->render('detail');
};

app->start;
__DATA__

@@ index.html.ep
% layout 'default';
% title 'Welcome to Strassenfeste';

%= form_for '/' => begin
    %= label_for filter_from_date => 'From Date'
    %= date_field 'filter_from_date'
    %= label_for filter_to_date => 'To Date'
    %= date_field 'filter_to_date'
    %= label_for filter_description => 'Description'
    %= text_field 'filter_description'
    %= submit_button 'OK'
% end

<table>
    <tr>
    % for my $a (@$attrs) {
          <th><%= $a %></td>
    % }
    <th>Detail</th>
    </tr>
    % for my $line (@$feste_data) {
        <tr>
          % for my $a (@$attrs) {
          <td><%= $line->{$a} %></td>
          % }
          <td><%= link_to Detail => 'detail/'. $line->{id} %></td>
        </tr>
    % }
</table>

@@detail.html.ep
% layout 'default';
% title 'Detail';
<table>
    % for my $a (@$detailattrs) {
        <tr>
          <td><%= $a %></td>
          <td><%= $fest->{$a} %></td>
        </tr>
    % }
</table>


@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
  <head><title><%= title %></title>
  <%= stylesheet '/style.css'%>
  <%= stylesheet '/css/ui-lightness/jquery-ui-1.10.3.custom.min.css'%>
  <%= javascript '/js/jquery-1.9.1.js'%>
  <%= javascript '/js/jquery-ui-1.10.3.custom.min.js'%>
  <%= javascript '/strassenfeste.js'%>
  </head>
  <body><%= content %></body>
</html>

@@ style.css
th
  {border: 1px solid black;
   background-color: grey;
  }
td
  {border: 1px solid black}
table
  {border-collapse: collapse}

@@ strassenfeste.js
$(function(){
    $('input[type=date]').datepicker({ dateFormat: "dd.mm.yy" });
});
