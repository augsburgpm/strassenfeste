#!/usr/bin/env perl

use Mojolicious::Lite;
use Mojo::JSON 'j';
use Memoize;

my $ger_date_re = qr/^ (?<tag>\d\d?)  [.] (?<monat>\d\d?) [.] (?<jahr>\d{4}) $/x;
my $int_date_re = qr/^ (?<jahr>\d{4}) [-] (?<monat>\d\d?) [-] (?<tag>\d\d?) $/x;

my $source_url = (app->mode eq 'development'
                  ? '/all.json'
                  :'http://www.berlin.de/sen/wirtschaft/service/maerkte/strassenfeste/index.php/index/all.json?q='
                 );
my $cache_source_url_times = 5;

app->secret('mojostrassenfeste');

sub parse_date {
    my ($date) = @_;
    return if !$date;
    if ($date =~ $ger_date_re || $date =~ $int_date_re) {
        return ($+{tag}, $+{monat}, $+{jahr});
    }
    else {
        return ();
    }
}

sub get_data {
    my @results = @{j(app->ua->get($source_url)->res->body)->{index}};

    for my $fest (@results) {
        for my $datename (qw(von bis)) {
            if (my ($tag, $monat, $jahr) = parse_date($fest->{$datename})) {
                $fest->{$datename} = sprintf('%02d.%02d.%04d', $tag, $monat, $jahr);
                $fest->{"__cmp__$datename"} = sprintf('%04d.%02d.%02d', $jahr, $monat, $tag);
            }
            else {
                die qq(invalid date in input for '$datename':'$fest->{$datename}');
            }
        }
    }
    return \@results;
}
memoize 'get_data';

sub get_filtered_data {
    my ($filters) = @_;
    state $get_data_counter = 0;
    if ($get_data_counter++ >= $cache_source_url_times) {
        Memoize::flush_cache('get_data');
        $get_data_counter = 0;
    }
    my $bare_data = get_data();
    return [grep {my $fest = $_;
                  my $ok = 1;
                  $ok *= $_->($fest) for @$filters;
                  $ok;
              }
            @$bare_data
           ];
}

# Documentation browser under "/perldoc"
plugin 'PODRenderer';

get '/' => sub {
  my $self = shift;

  my @filters;
  if (my ($tag, $monat, $jahr) = parse_date($self->param('filter_from_date'))) {
      my $cmp_with = sprintf('%04d.%02d.%02d', $jahr, $monat, $tag);
      push @filters, sub {$_[0]->{__cmp__bis} ge $cmp_with};
  }
  if (my ($tag, $monat, $jahr) = parse_date($self->param('filter_to_date'))) {
      my $cmp_with = sprintf('%04d.%02d.%02d', $jahr, $monat, $tag);
      push @filters, sub {$_[0]->{__cmp__von} le $cmp_with};
  }

  if (my $desc = $self->param('filter_description')) {
      push @filters, sub {index(lc($_[0]->{bezeichnung}), lc($desc)) >= 0};
  }
  $self->stash(feste_data => get_filtered_data(\@filters));
  $self->stash(attrs => [qw[von bis bezeichnung]]);
  $self->render('index');
};

get '/detail/:id' => sub {
    my $self = shift;
    my $id = $self->stash('id');

    my ($fest) = @{get_filtered_data([sub {$_[0]->{id} == $id}])};
    $self->stash('fest', $fest);
    $self->stash(detailattrs => [qw[bezirk bezeichnung strasse plz von bis zeit veranstalter]]);

    $self->render('detail');
};

app->start;
__DATA__

@@ index.html.ep
% layout 'default';
% title 'Welcome to Strassenfeste';

%= form_for '/' => begin
    %= label_for filter_from_date => 'Welches Straßenfest kann ich zwischen dem'
    %= date_field 'filter_from_date'
    %= label_for filter_to_date => 'und dem'
    %= date_field 'filter_to_date'
    besuchen?
    %= label_for filter_description => 'Suche in der Beschreibung nach:'
    %= text_field 'filter_description'
    %= submit_button 'OK'
% end

<table>
    <tr>
    % for my $a (@$attrs) {
          <th><%= $a %></td>
    % }
    <th>Detail</th>
    </tr>
    % for my $line (@$feste_data) {
        <tr>
          % for my $a (@$attrs) {
          <td><%= $line->{$a} %></td>
          % }
          <td><%= link_to Detail => 'detail/'. $line->{id} %></td>
        </tr>
    % }
</table>

@@detail.html.ep
% layout 'default';
% title 'Detail';
<p><%= link_to 'Zurück' => '/'%></p>
<table>
    % for my $a (@$detailattrs) {
        <tr>
          <td><%= $a %></td>
          <td><%= $fest->{$a} %></td>
        </tr>
    % }
</table>


@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
  <head><title><%= title %></title>
  <%= stylesheet '/style.css'%>
  <%= stylesheet '/css/ui-lightness/jquery-ui-1.10.3.custom.min.css'%>
  <%= javascript '/js/jquery-1.9.1.js'%>
  <%= javascript '/js/jquery-ui-1.10.3.custom.min.js'%>
  <%= javascript '/strassenfeste.js'%>
  </head>
  <body><%= content %></body>
</html>

@@ style.css
th
  {border: 1px solid black;
   background-color: grey;
  }
td
  {border: 1px solid black}
table
  {border-collapse: collapse}

@@ strassenfeste.js
$(function(){
    $('input[type=date]').datepicker({ dateFormat: "dd.mm.yy" });
});
